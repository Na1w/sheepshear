#####################################################
#
# SheepShear, A PowerPC Mac emulator
# Forked from SheepShaver
#
# scons build script, all roads lead to Rome
# 2012, Alexander von Gluck
#
#####################################################
import SCons
import os
import platform
import sys

# Add a dependency to build system
def depend(environment, package):
	print 'Checking for ' + package + '...'
	environment.ParseConfig('pkg-config --cflags --libs ' + package)
	return;

machineOS = platform.system()
machineCPU = platform.machine()

# Common build environment items
env = Environment()
env.Append(CPPPATH = [
	'#/src/include',
	'#/src/kpx_cpu/include',
	'#/src/kpx_cpu/src',
	'#/src/slirp',
	'#/build-release'])

cppdefines = ['_REENTRANT']
cppdefines += ['DATADIR=\\"/usr/share/SheepShear\\"']
cppdefines += ['REAL_ADDRESSING=1']

# Common source code
source_code = Glob('#/src/*.cpp')
source_code += Glob('#/src/slirp/*.c')

# TODO: Only non-jit for the moment
cpu_code = ['#/src/kpx_cpu/src/mathlib/ieeefp.cpp', '#/src/kpx_cpu/src/mathlib/mathlib.cpp',
	'#/src/kpx_cpu/src/cpu/ppc/ppc-cpu.cpp', '#/src/kpx_cpu/src/cpu/ppc/ppc-decode.cpp',
	'#/src/kpx_cpu/src/cpu/ppc/ppc-execute.cpp', '#/src/kpx_cpu/src/cpu/ppc/ppc-translate.cpp',
	'#/src/kpx_cpu/src/utils/utils-cpuinfo.cpp', '#/src/kpx_cpu/sheepshaver_glue.cpp']

if machineOS in ('Linux', 'FreeBSD'):
	cppdefines += ['HAVE_SIGINFO_T']
	env.Append(CPPPATH = ['#src/Unix', '#/src/include/platform/Unix'])
	env.Append(LIBS = ['rt'])
	cppdefines += ['HAVE_SIGINFO_T']
	source_code += Glob('#/src/Unix/*.cpp')
	source_code += Glob('#/src/Unix/*.c')
	source_code += Glob('#/src/Unix/Linux/*.cpp')
	source_code += Glob('#/src/dummy/prefs_dummy.cpp')
	depend(env, 'gtk+-2.0')
	depend(env, 'x11')
	depend(env, 'xext')
	depend(env, 'xxf86dga')
	depend(env, 'xxf86vm')
elif machineOS in ('Darwin'):
	print "TODO: MacOS X Support"
	sys.exit(1)
elif machineOS in ('Haiku'):
	print "TODO: Haiku Support"
	sys.exit(1)
else:
	print "Unknown operating system!"
	sys.exit(1)

env.Append(CPPDEFINES = cppdefines)
ppc_cpu_impl = env.Command('ppc-execute-impl.cpp', '#/src/kpx_cpu/src/cpu/ppc/ppc-decode.cpp',
	'$CC -E $_CPPINCFLAGS -DGENEXEC $SOURCE | /usr/bin/perl src/kpx_cpu/src/cpu/ppc/genexec.pl > $TARGET')

dyngen = env.Program('dyngen', ['./kpx_cpu/src/cpu/jit/dyngen.c','./kpx_cpu/src/cpu/jit/cxxdemangle.cpp'])
#env.Command('src-list.txt', dyngen, '$SOURCE $SRCGEN_ARGS > $TARGET')
sheepshear = env.Program('SheepShear', source_code + cpu_code)
Depends(sheepshear, ppc_cpu_impl)
Decider('MD5')
