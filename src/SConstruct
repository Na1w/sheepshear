#####################################################
#
# SheepShear, A PowerPC Mac emulator
# Forked from SheepShaver
#
# scons build script, all roads lead to Rome
# 2012, Alexander von Gluck
#
#####################################################
import SCons
import os
import platform
import sys

machineOS = platform.system()
machineCPU = platform.machine()

env = Environment()
env.Append(CPPPATH = [
	'./include',
	'./kpx_cpu/include',
	'./kpx_cpu/src',
	'./slirp'])

cppdefines = ['_REENTRANT']
cppdefines += ['DATADIR=\\"/usr/share/SheepShear\\"']

system_code = ['emul_op.cpp', 'ether.cpp', 'extfs.cpp', 'gfxaccel.cpp',
	'macos_util.cpp', 'main.cpp', 'prefs.cpp', 'prefs_items.cpp',
	'rom_patches.cpp', 'rom_toolbox.cpp', 'rsrc_patches.cpp',
	'./kpx_cpu/sheepshaver_glue.cpp', 'thunks.cpp', 'user_strings.cpp']
driver_code = ['adb.cpp', 'audio.cpp', 'cdrom.cpp', 'disk.cpp',
	'name_registry.cpp', 'scsi.cpp', 'serial.cpp', 'sony.cpp',
	'timer.cpp', 'video.cpp', 'xpram.cpp']
ppc_cpu_code = Glob('./kpx_cpu/src/cpu/ppc/*.cpp')

if machineOS in ('Linux', 'FreeBSD'):
	env.Append(CPPPATH = ['./Unix', './include/platform/Unix'])
	env.Append(_LIBFLAGS = ['-lrt'])
	cppdefines += ['HAVE_SIGINFO_T']
	platform_driver_code = Glob('./Unix/*.cpp')
	env.ParseConfig('pkg-config --cflags --libs gtk+-2.0')
	env.ParseConfig('pkg-config --cflags --libs x11')
	env.ParseConfig('pkg-config --cflags --libs xext')
	env.ParseConfig('pkg-config --cflags --libs xxf86dga')
	env.ParseConfig('pkg-config --cflags --libs xxf86vm')
elif machineOS in ('Darwin'):
	print "TODO: MacOS X Support"
	sys.exit(1)
elif machineOS in ('Haiku'):
	print "TODO: Haiku Support"
	sys.exit(1)
else:
	print "Unknown operating system!"
	sys.exit(1)

env.Append(CPPDEFINES = cppdefines)
env.Program('SheepShear', system_code + driver_code + ppc_cpu_code + platform_driver_code)
